/*
                                                                                                                                  _,o9   
                                                                                                                                ,'       
    `7MM"""YMM  `7MN.   `7MF'MMP""MM""YMM  .M"""bgd      .M"""bgd   .g8""8q. `7MMF'   `7MF'`7MM"""YMM `7MM"""YMM `7MMF'      `7MM"""YMM  
      MM    `7    MMN.    M  P'   MM   `7 ,MI    "Y     ,MI    "Y .dP'    `YM. MM       M    MM    `7   MM    `7   MM          MM    `7  
      MM   d      M YMb   M       MM      `MMb.         `MMb.     dM'      `MM MM       M    MM   d     MM   d     MM          MM   d    
      MMmmMM      M  `MN. M       MM        `YMMNq.       `YMMNq. MM        MM MM       M    MM""MM     MM""MM     MM          MMmmMM    
      MM   Y  ,   M   `MM.M       MM      .     `MM     .     `MM MM.      ,MP MM       M    MM   Y     MM   Y     MM      ,   MM   Y  , 
      MM     ,M   M     YMM       MM      Mb     dM     Mb     dM `Mb.    ,dP' YM.     ,M    MM         MM         MM     ,M   MM     ,M 
    .JMMmmmmMMM .JML.    YM     .JMML.    P"Ybmmd"      P"Ybmmd"    `"bmmd"'    `bmmmmd"'  .JMML.     .JMML.     .JMMmmmmMMM .JMMmmmmMMM 
                                                                                                                  
                   
                                                   ```.......```                                                    
                                           `--///+++:.-..://++//+:::::///////-````                                  
                                       `:/sso+/++yyoso+:/+/:/o++ssoooso/ss+oso+o+/:-.`````                          
                                    `-/o+so++++o::++++oso++o++os+//o++oooo+/:-:/+ooo+osoo+/:-.                      
                                 `..:y+//++/:..``./+:://oooso////os+syysooysyyyy+//--://sssys+/-                    
                               `--`-:+.syo+o+/::/++//+++oo+o+::osyyyyyssyo+o/::+syso+//:-+syss+--...                
                        `.-----+.````.:+//++ys++/oysoos++o+///+/--::+oossyyo++os++o+oos//+o+++:.````.`              
                      `-o/.``./..```````.:/++//+/++++ooo+++-.+++ooysso+syysssyyo/so+++o++:::.````````.--`           
                     -/os:``:+..````````````..-.-////osyssooyo//os+/++o/++/oysosoo/::::-..`````````````-/--.        
                     ---..+s-`.````````````````````-:/://+/os+//----.``::+++//-.``````````````````````.`:+``..`     
                    .:.``-y/````````````````````````-----::/:-----.```````````````````````````````````.`.y:``.::`   
                   --`.`.+-````````````````````````````````````````````````````````````````.````````.```-ss.````::  
                  --..`-+s+---..--.````````````````````````````````````````````````````````````````....:o:``:````-- 
                 .-`-/+syys+/-.-:---.`````.....````````````````````````````````````````````.-...----:-::````.+://.: 
                 .-`-o::ssysooysoo:``.+:---.----...-----````````````````````````````.-///++/.```````.`.``.``.:syss/ 
                  .-`-o...-os//soo.``-o.://.-````````...so+-:---:.`````..-:::/+/:/+o+:.```.os`-:../s++o+++/+so/+ys- 
                   `--`````:..-o-..-+:-+y+so+++sss++++/..o+:y+...-///oo.::+/soossys+///:.``/+ossoyyossysooo/.:+o+:` 
                      .:::----```-+o.-osysyo+osooooo+++:`o/oso/+--.`-os`````-./sso/-.``````/yyyyso/://-://+oo/-.`   
                         `.:y/::.```./+o+yso++/ssosoooo//osyoso+o+/--`.````.``.``...`````..s+/--.::/+o++s+-.`       
                            ss+....--.```:///+oo+++o++o++/:.::/s+oso-.:--:/..```````````+y+//+oso++ysys/-.          
                            /o/-````.//::::-.````.``..```..---..-::/os+o+:...`..-.....-+ysys+/:.```:-:-//           
                            -o+:`````+s+-``.-:--------....````.----.```.````.---.```````..---```````.:+/-           
                            `/+/.````oss+`````````````````.:```````......--..``-:+s+`````-/+s-``````./+o.           
                            `///:````/yo-```````-o:```````.:-.```````:++:``````+yd/o`````//oo.``````:/oy            
                             /++:.```-s:-```````.+-````````.:-``````-:://`````:+++//````-/+/s```````-os/            
                             :+:/:```.s--```````.o:````````./.`````-/:::+`````-/+:+-````.:/:/```````:s/-            
                             .+/:-```.y+-.```````o:.````````+.```````-:/+`````::::+.````.:/o:``````.+o:`            
                             `/o/:````s//:```````s/.````````/-```````:+++````.:/+-+`````:/+d.``````/+::             
                              /+/:````o/:-```````o:-```````./-```````-+:/````.:::++`````.:+y``````-//.-             
                              :h/-.```+:::```````s+:```````.+-````````///````.-://-`````-/o+``````.+o/.             
                              .ho/-```+:/:.``````o::.``````.+.```````.:+/```./++o+-````-://:`````-/o/:`             
                               sho/.``/+//-``````o/--```````:.```````./o:```.+os:/.```.::+/-````.+o+//              
                               `-+sys+:-::-``````+/.-.`````.:-```````./+:```.:/+/+````-//oo.```./oo/-.              
                                   `--:///+/-````-//:-````..--```````//-.```-/+o/````./+/:.``-:/:-.                 
                                          ``.---::/+oo+++/::--:+++++//:----:/+/:.....-----....`                     
                                                    ``.---........-:------............
                                                      
                                   
                                                   A cloud of clouds; collective noun                             
                                                                                                    
 */

#include <Encoder.h>
#include <Wire.h> 
#include <LiquidCrystal_I2C.h>
#include <ESP8266WiFi.h>
#include <WiFiUdp.h>

// Configuration constants
#define WIFI_SSID "ENTS"
#define WIFI_PSK "hacktheplanet"
IPAddress MULTICAST_IP(225, 225, 225, 225);
#define MULTICAST_PORT 2525
#define WIFI_WAIT_MAX 30000 // 30 seconds
#define SATURATION 1.0 // 0.0 - 1.0
#define VALUE 1.0 // 0.0 - 1.0

// General constants
#define DEBUG_SERIAL Serial
#define LED_PIN D4
#define LCD_SDA_PIN D1
#define LCD_SCL_PIN D2
#define ENC_CLK_PIN D5
#define ENC_DT_PIN D6
#define ENC_SW_PIN D3
#define ENC_SW_OPEN 1 // inverted due to pullup
#define ENC_SW_CLOSED 0
#define MAX_MODES 7

// State variables
int red;
int green;
int blue;
int currentMode = -1; // intentionally invalid: wait for mode before starting code
long lastEncPosition = 0;
double deg = 0;
int lastSwState = ENC_SW_OPEN;
bool justUpdatedMode = true;
bool justChangedMode = true;
bool startedOk = false;

// Protocol flags
#define PROTOCOL_CHANGE_MODE 0x01
#define PROTOCOL_INCREMENT_MODE 0x02
#define PROTOCOL_PARAMS_COLOR 0xA0

// Operational variables
Encoder enc(ENC_CLK_PIN, ENC_DT_PIN);
LiquidCrystal_I2C lcd(0x27, 16, 2);  // set the LCD address to 0x27 for a 16 chars and 2 line display
WiFiUDP udp;
byte packetBuffer[512];

// define prototypes
void checkColorMode();
void writeLCD(String line1, String line2);
void updateColor();
void hsv2rgb(float hueDegrees, float saturation, float value, int rgb[]);
 
void setup() {
  // debugging
  DEBUG_SERIAL.begin(9600);

  // general IO setup
  pinMode(LED_PIN, OUTPUT);

  // setup lcd
  Wire.begin(LCD_SDA_PIN, LCD_SCL_PIN);
  lcd.init();
  lcd.backlight();
  writeLCD("ENTS Souffle", "Loading...");
  
  // connect to wifi
  DEBUG_SERIAL.println("Connecting to wifi...");
  WiFi.begin(WIFI_SSID, WIFI_PSK);

  long wifiStart = millis();
  while(WiFi.status() != WL_CONNECTED){
    DEBUG_SERIAL.println("Waiting for connection to wifi...");
    digitalWrite(LED_PIN, LOW);
    delay(500);
    digitalWrite(LED_PIN, HIGH);
    delay(500);
    if(millis() - wifiStart > WIFI_WAIT_MAX){
      DEBUG_SERIAL.println("Failed to connect to wifi, cancelling startup");
      startedOk = false;
      return;
    }
  }
  DEBUG_SERIAL.println("Connected to wifi");
  DEBUG_SERIAL.print("IP: ");
  DEBUG_SERIAL.println(WiFi.localIP());

  // join multicast
  udp.beginMulticast(WiFi.localIP(), MULTICAST_IP, MULTICAST_PORT);
  
  digitalWrite(LED_PIN, HIGH);
  pinMode(ENC_SW_PIN, INPUT);
  startedOk = true;
}

void loop() {
  // check encoder
  long newPosition = enc.read();
  if(newPosition != lastEncPosition){
    // Update degrees by number of rotations
    // Approx 80 values per revolution, so ~3 revolutions for 360 degrees
    double diff = newPosition - lastEncPosition;
    deg += diff * 1.0;
    lastEncPosition = newPosition;

    // clamp degree range
    if(deg > 360){
      deg = 0;
    }else if(deg < 0){
      deg = 360;
    }
  }

  // check for mode change
  int swState = digitalRead(ENC_SW_PIN);
  if(swState != lastSwState){
    delay(10); // debounce
    swState = digitalRead(ENC_SW_PIN);
    lastSwState = swState;

    if(swState == ENC_SW_OPEN){
      udp.beginPacketMulticast(MULTICAST_IP, MULTICAST_PORT, WiFi.localIP(), 1200);
      udp.write(PROTOCOL_INCREMENT_MODE);
      udp.endPacket();
    }
  }

  // read udp
  int packetSize = udp.parsePacket();
  if(packetSize){
    DEBUG_SERIAL.println("Received packet...");
    DEBUG_SERIAL.println("  Length: " + String(packetSize));
    DEBUG_SERIAL.print("  From: ");
    DEBUG_SERIAL.println(udp.remoteIP());
    DEBUG_SERIAL.println("  Port: " + String(udp.remotePort()));

    int len = udp.read(packetBuffer, 255);
    if(len > 0) {
      packetBuffer[len] = 0;
    }

    if(packetBuffer[0] = PROTOCOL_CHANGE_MODE){
      int newMode = (int)packetBuffer[1];
      if(newMode >= 0 && newMode < MAX_MODES){
        justChangedMode = currentMode != newMode;
        currentMode = newMode;
        justUpdatedMode = true;
      }
    }
  }

  switch(currentMode){
    case 0: 
      checkColorMode(); 
      if(justChangedMode){
        writeLCD("Mode: RGB Color", "Turn to adjust");
      }
      break;
    // TODO: Other modes
    case 6: 
      if(justChangedMode){
        writeLCD("Mode: Off", "");
      }
      break;
  }

  justUpdatedMode = false;
  justChangedMode = false;
}

void checkColorMode(){
  int r1 = red;
  int g1 = green;
  int b1 = blue;

  updateColor();

  if(r1 != red || g1 != green || b1 != blue || justUpdatedMode){
    DEBUG_SERIAL.println("MODE: Color. R = " + String(red) + ", G = " + String(green) + ", B = " + String(blue));
    udp.beginPacketMulticast(MULTICAST_IP, MULTICAST_PORT, WiFi.localIP(), 1200);
    udp.write((byte)PROTOCOL_PARAMS_COLOR);
    udp.write((byte)red);
    udp.write((byte)green);
    udp.write((byte)blue);
    udp.endPacket();
  }
}

void writeLCD(String line1, String line2){
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print(line1);
  lcd.setCursor(0, 1);
  lcd.print(line2);
}

void updateColor() {
  int rgb[3];
  hsv2rgb(deg, SATURATION, VALUE, rgb);
  
  red = rgb[0];
  blue = rgb[1];
  green = rgb[2];
}

// hueDegrees: 0 - 360
// saturation: 0 - 1
// value: 0 - 1
void hsv2rgb(float hueDegrees, float saturation, float value, int rgb[]) {
  hueDegrees = constrain(hueDegrees, 0.0, 360.0) / 360; // convert to 0 - 1 from 0 - 360
  saturation = constrain(saturation, 0.0, 1.0);
  value = constrain(value, 0.0, 1.0);

  // Conversion adapted from https://github.com/ratkins/RGBConverter/blob/7413c6dce023943f8c89df343b3c027bbf29a86c/RGBConverter.cpp#L131

  int i = int(hueDegrees * 6);
  double f = hueDegrees * 6 - i;
  double p = value * (1 - saturation);
  double q = value * (1 - f * saturation);
  double t = value * (1 - (1 - f) * saturation);

  double r;
  double g;
  double b;

  switch(i % 6){
  case 0: r = value; g = t; b = p; break;
  case 1: r = q; g = value; b = p; break;
  case 2: r = p; g = value; b = t; break;
  case 3: r = p; g = q; b = value; break;
  case 4: r = t; g = p; b = value; break;
  case 5: r = value; g = p; b = q; break;
  }

  rgb[0] = r * 255;
  rgb[1] = g * 255;
  rgb[2] = b * 255;
}
